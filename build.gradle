buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
		maven { url "https://repo.spring.io/libs-release/" }
		mavenCentral()
		maven { url "https://repo.spring.io/milestone/" }
		maven { url "https://ajoberstar.org/bintray-backup/" }
	}
}

plugins {
	id 'org.ajoberstar.reckon' version '0.16.1'
	id 'org.springframework.boot' version '2.7.1'
	id "io.spring.dependency-management" version "1.0.12.RELEASE"
	id 'com.google.cloud.tools.jib' version '3.2.1'
	id 'java'
	id "application"
}

group = 'io.dnsmonitor.dns'

sourceCompatibility = 1.17
targetCompatibility = 1.17

repositories {
	maven { url "https://repo.spring.io/libs-release" }
	mavenCentral()
	maven { url "https://repo.spring.io/milestone/" }
}

application {
	mainClass.set("io.dnsmonitor.dns.client.DnsClientApplication")
}

jar {
	enabled = false
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'dnsjava:dnsjava:3.5.1'
	implementation 'org.apache.commons:commons-text:1.9'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

reckon {
	stages('rc', 'final')

	// how do you calculate the scope/stage
	scopeCalc = calcScopeFromProp().or(calcScopeFromCommitMessages()) // fall back to commit message (see below) if no explicit prop set
	stageCalc = calcStageFromProp()
	tagWriter = version -> "v" + version
}

jib {
	from {
		image = 'eclipse-temurin:17-focal'
	}
	to {
		image = 'europe-docker.pkg.dev/dnsmonitor/containers/dns-client'
		project.afterEvaluate {
			def reconVersion = version.toString()
			if (!reconVersion.contains("-rc")) {
				tags = [version.toString(), 'latest']
			} else {
				tags = ['latest']
			}
		}
	}
	container {
		mainClass = "io.dnsmonitor.dns.client.DnsClientApplication"
		ports = ['8001']
		format = 'OCI'

		creationTime = 'USE_CURRENT_TIMESTAMP'
	}
	allowInsecureRegistries = false
}
